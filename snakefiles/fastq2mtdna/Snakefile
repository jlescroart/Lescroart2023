### Snakefile for fastq2mtdna pipeline, with associated file config.yaml
### Mapping of raw reads (FASTQ) to reference mitogenome, output in fasta
### Load associated environment with $ conda activate fastq2mtdna
### Author Jonas Lescroart
### snakemake -p --cores 4
### The interleaved fastq as intermediate file is very large per sample (100-200 GB)
### So limit the amount of parallelization through the provided cores

### Packages
import pandas as pd
import re
from Bio import SeqIO
import os.path

### Configuration
configfile: "config_mLynCan4.pri.v2.yaml"

### Functions
def strip_ext(file_str): 
    bits = file_str.split(".") 
    if re.match("gz", bits[-1]): 
        bits = bits[:-1] 
    if re.match("fasta|fa|fna", bits[-1]): 
        bits = bits[:-1] 
    return ".".join(bits) 

def get_fq(some_id, read = 12):
    fq = "{dir}/{reads}".format(dir = metadata["path"].loc[some_id], reads = metadata["read_files"].loc[some_id])
    fq1 = fq.format("1")
    fq2 = fq.format("2")
    if read == 1:
        return [fq1]
    elif read == 2:
        return [fq2]
    elif read == 12:
        return [fq1, fq2] 

### Variables
metadata = pd.read_csv(config["metadata"], sep = "\t", engine = "python", encoding="latin-1").set_index("unique_id", drop = False)
unique_id = [id for id in metadata["unique_id"].astype(str).tolist()]

unique_id.remove("mLynCan4") # Temporary skipping Lynx bc data is faulty
unique_id.remove("bLge-252")

### Rules
rule all:
    input:
        "{dir}/mitogenome_unaligned.info".format(dir = config["output"]["mtdna"]),
        "{dir}/mitogenome_raxml_bs.nwk".format(dir = config["output"]["mtdna"])

rule gunzip:
    input:
        gz = config["ref"]["file"]
    output:
        fna = temp(strip_ext(config["ref"]["file"]) + ".fna")
    shell:
        "gunzip -c {input.gz} > {output.fna}"

rule mitogenome:
    input:
        fna = strip_ext(config["ref"]["file"]) + ".fna"
    output:
        fa = "{dir}/".format(dir = config["output"]["mtdna"]) + config["ref"]["assembly"] + "_mitogenome.fa" 
    params:
        mitogenome = config["ref"]["mitogenome"]
    run:
        record_dict = SeqIO.index(input.fna, "fasta")
        mtdna = record_dict[params.mitogenome]
        with open(output.fa, 'w') as outfile:
            SeqIO.write(mtdna, outfile, "fasta")

rule interleave:
    input:
        fastq_gz = lambda wildcards: get_fq(wildcards.id, 12) 
    output:
        fastq = temp("{dir}/{{id}}_interleaved.fastq".format(dir = config["output"]["mtdna"]))
    params:
        script = config["scripts"]["interleave"]
    shell:
        "python2 {params.script} {input.fastq_gz} > {output.fastq}"

rule mitobim:
    input:
        fastq = "{dir}/{{id}}_interleaved.fastq".format(dir = config["output"]["mtdna"]),
        fa = "{dir}/".format(dir = config["output"]["mtdna"]) + config["ref"]["assembly"] + "_mitogenome.fa"
    output:
        fasta = temp("{dir}/{{id}}_bb.fasta".format(dir = config["output"]["mtdna"]))
    params:
        mira = config["software"]["mira"],
        mitobim = config["software"]["mitobim"],
        figure_id = lambda wildcards: metadata.loc[wildcards.id]["figure_id"],
        ref = config["ref"]["assembly"],
        sample_dir = lambda wildcards: config["output"]["mtdna"] + "/" + wildcards.id + "_mitobim"
    run:
        # Set up dir and prep environment
        shell("mkdir -p {params.sample_dir}")
        os.chdir(params.sample_dir)
        shell("export LC_ALL=C") # To fix a bug in the MIRA assembler used by MITObim
        # Run MITObim
        shell("{params.mitobim} --mirapath {params.mira} -end 10 --trimreads -sample {params.figure_id} -ref {params.ref} -readpool {input.fastq} --quick {input.fa}")
        # Copy fasta output from last iteration and clean up iteration files
        last = sorted(os.listdir(params.sample_dir))[-1]
        target = params.sample_dir + "/" + last + "/" + params.figure_id + "-" + params.ref + "-it" + last[-1] + "_noIUPAC.fasta"
        mtdna = SeqIO.read(target, "fasta")
        shell("mv {target} {output.fasta}")
        shell("rm -r {params.sample_dir}")

rule record_id:
    input:
        fasta = "{dir}/{{id}}_bb.fasta".format(dir = config["output"]["mtdna"])
    output:
        fasta = "{dir}/{{id}}.fasta".format(dir = config["output"]["mtdna"])
    params:
        mitogenome = config["ref"]["mitogenome"]
    run:
        record = SeqIO.read(input.fasta, "fasta")
        record.id = params.mitogenome
        record.name = ""
        record.description = ""
        SeqIO.write(record.upper(), output.fasta, "fasta")

rule createWindow_aln:
    input:
        fasta = expand("{dir}/{{id}}.fasta".format(dir = config["output"]["mtdna"]), id = unique_id)
    output:
        fasta = temp("{dir}/mitogenome_unaligned_unique_id.fasta".format(dir = config["output"]["mtdna"]))
    params:
        cwd = config["output"]["mtdna"],
        script = config["scripts"]["createWindow_aln"],
        fasta = "{dir}/windows/".format(dir = config["output"]["mtdna"]) + config["ref"]["mitogenome"] + ".fasta",
        dir = "{dir}/windows/".format(dir = config["output"]["mtdna"]),
        txt = "{dir}/filenames.txt".format(dir = config["output"]["mtdna"])
    shell:
        "cd {params.cwd} && " +
        "python {params.script} {input.fasta} && " +
        "mv {params.fasta} {output.fasta} && " +
        "rm -r {params.dir} && rm {params.txt}"

rule unique_id2figure_id:
    input:
        fasta = "{dir}/mitogenome_unaligned_unique_id.fasta".format(dir = config["output"]["mtdna"])
    output:
        fasta = "{dir}/mitogenome_unaligned.fasta".format(dir = config["output"]["mtdna"])
    run:
        with open(output.fasta, "w") as out:
            for line in shell("cat {input.fasta}", iterable = True):
                for id in unique_id:
                    line = re.sub(id, metadata["figure_id"][id], line)
                out.write(line + "\n")

rule diagnose_fasta:
    input:
        fasta = "{dir}/mitogenome_unaligned.fasta".format(dir = config["output"]["mtdna"])
    output:
        info = "{dir}/mitogenome_unaligned.info".format(dir = config["output"]["mtdna"])
    params:
        script = config["scripts"]["diagnose_fasta"]
    shell:
        "python {params.script} {input.fasta}"

rule muscle:
    input:
        fasta = "{dir}/mitogenome_unaligned.fasta".format(dir = config["output"]["mtdna"])
    output:
        fasta = "{dir}/mitogenome_alignment.fasta".format(dir = config["output"]["mtdna"])
    threads: 4
    shell:
        "muscle -threads {threads} -align {input.fasta} -output {output.fasta}"

rule raxml:
    input:
        fasta = "{dir}/mitogenome_alignment.fasta".format(dir = config["output"]["mtdna"])
    output:
        nwk = "{dir}/mitogenome_raxml_bs.nwk".format(dir = config["output"]["mtdna"]),
        log = "{dir}/mitogenome_raxml_bs.log".format(dir = config["output"]["mtdna"])
    threads: 4
    params:
        outdir = config["output"]["mtdna"],
        outgroup = metadata.loc[config["outgroup"]]["figure_id"],
        tag = "temp"
    shell:
        "raxmlHPC-PTHREADS -T {threads} -m GTRGAMMA -f a -N 1000 -p 8 -x 8 -o {params.outgroup} -s {input.fasta} -w {params.outdir} -n {params.tag} && " +
        "mv {params.outdir}/RAxML_bipartitions.{params.tag} {output.nwk} && " +
        "mv {params.outdir}/RAxML_info.{params.tag} {output.log} && " +
        "rm {params.outdir}/RAxML_bipartitionsBranchLabels.{params.tag} {params.outdir}/RAxML_bestTree.{params.tag} {params.outdir}/RAxML_bootstrap.{params.tag}"


